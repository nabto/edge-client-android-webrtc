apply plugin: 'maven-publish'
apply plugin: 'signing'

def getReleaseRepositoryUrl() {
    return 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId POM_GROUP_ID
                artifactId POM_ARTIFACT_ID
                version POM_VERSION

                from components.release
                pom {
                    name = POM_ARTIFACT_ID
                    description = 'Nabto Edge WebRTC for Android clients.'
                    url = 'https://github.com/nabto/edge-client-android-webrtc'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'nabto'
                            name = 'Nabto'
                            email = 'apps@nabto.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:nabto/edge-client-android-webrtc.git'
                        developerConnection = 'scm:git@github.com:nabto/edge-client-android-webrtc.git'
                        url = 'https://github.com/nabto/edge-client-android-webrtc'
                    }
                }
            }
        }

        repositories {
            maven {
                url = rootProject.ext["isReleaseVersion"] ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()
                credentials {
                    username = rootProject.ext["ossrhUsername"]
                    password = rootProject.ext["ossrhPassword"]
                }
            }
        }
    }
}

signing {
    def signingKeyB64 = rootProject.ext["signingKeyBase64"]
    def signingPassword = rootProject.ext["signingPassword"]
    def signingKey = new String(Base64.decoder.decode(signingKeyB64))
    useInMemoryPgpKeys(
        signingKey,
            signingPassword,
    )
    sign publishing.publications
}

